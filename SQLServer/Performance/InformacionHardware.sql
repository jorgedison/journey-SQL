USE MASTER
GO

-- 1. HARDWARE INFORMATION FROM SQL SERVER 2012 
SELECT 	CPU_COUNT AS [LOGICAL CPU COUNT], SCHEDULER_COUNT, 
		HYPERTHREAD_RATIO AS [HYPERTHREAD RATIO],
		CPU_COUNT/HYPERTHREAD_RATIO AS [PHYSICAL CPU COUNT], 
		PHYSICAL_MEMORY_KB/1024 AS [PHYSICAL MEMORY (MB)], 
		COMMITTED_KB/1024 AS [COMMITTED MEMORY (MB)],
		COMMITTED_TARGET_KB/1024 AS [COMMITTED TARGET MEMORY (MB)],
		MAX_WORKERS_COUNT AS [MAX WORKERS COUNT], 
		AFFINITY_TYPE_DESC AS [AFFINITY TYPE], 
		SQLSERVER_START_TIME AS [SQL SERVER START TIME], 
		VIRTUAL_MACHINE_TYPE_DESC AS [VIRTUAL MACHINE TYPE]  
FROM SYS.DM_OS_SYS_INFO WITH (NOLOCK) OPTION (RECOMPILE);

-- 2. FILE NAMES AND PATHS FOR TEMPDB AND ALL USER DATABASES IN INSTANCE
SELECT DB_NAME([DATABASE_ID]) AS [DATABASE NAME], 
       [FILE_ID], 
	   NAME, 
	   PHYSICAL_NAME, 
	   TYPE_DESC, 
	   STATE_DESC,
	   IS_PERCENT_GROWTH, 
	   GROWTH,
	   CONVERT(BIGINT, GROWTH/128.0) AS [GROWTH IN MB], 
       CONVERT(BIGINT, SIZE/128.0) AS [TOTAL SIZE IN MB]
FROM SYS.MASTER_FILES WITH (NOLOCK)
WHERE [DATABASE_ID] > 4 
AND [DATABASE_ID] <> 32767
--OR [DATABASE_ID] = 2
--AND DB_NAME([DATABASE_ID]) = 'NAME DATABASE'
ORDER BY DB_NAME([DATABASE_ID]) OPTION (RECOMPILE);

-- 3. VOLUME INFO FOR ALL LUNS THAT HAVE DATABASE FILES ON THE CURRENT INSTANCE
SELECT 
		DISTINCT VS.VOLUME_MOUNT_POINT, 
		VS.FILE_SYSTEM_TYPE, 
		VS.LOGICAL_VOLUME_NAME, 
		CONVERT(DECIMAL(18,2),
		VS.TOTAL_BYTES/1073741824.0) AS [TOTAL SIZE (GB)],
		CONVERT(DECIMAL(18,2),
		VS.AVAILABLE_BYTES/1073741824.0) AS [AVAILABLE SIZE (GB)],  
		CAST(CAST(VS.AVAILABLE_BYTES AS FLOAT)/ CAST(VS.TOTAL_BYTES AS FLOAT) AS DECIMAL(18,2)) * 100 AS [SPACE FREE %]
FROM SYS.MASTER_FILES AS F WITH (NOLOCK)
CROSS APPLY SYS.DM_OS_VOLUME_STATS(F.DATABASE_ID, F.[FILE_ID]) AS VS OPTION (RECOMPILE);
